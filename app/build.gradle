buildscript {
    ext.kotlin_version = '1.2.41' // https://github.com/JetBrains/kotlin

    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:3.3.0' // https://bintray.com/android/android-tools/com.google.gms.google-services/
        classpath 'io.fabric.tools:gradle:1.25.3' // https://docs.fabric.io/android/changelog.html
    }
}

ext {
    version = '0.5.1'
}

repositories {
    google()
    jcenter()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 27
        def split = project.ext.version.toString().split('\\.')
        versionCode = (split[0].toInteger() << 16) + (split[1].toInteger() << 8) + (split[2].toInteger())
        versionName project.ext.version.toString()
    }

    signingConfigs {
        release {
            def localProperties = new Properties()
            localProperties.load(new FileInputStream(project.file('local.properties')))
            storeFile file(localProperties['storeFile'])
            storePassword localProperties['storePassword']
            keyAlias localProperties['keyAlias']
            keyPassword localProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.release
            ext.enableCrashlytics = false
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'com.android.support:design:27.1.1' // https://developer.android.com/topic/libraries/support-library/revisions
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:preference-v14:27.1.1'
    implementation 'com.google.firebase:firebase-core:15.0.2' // https://firebase.google.com/support/release-notes/android
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.2@aar') { transitive = true } // https://docs.fabric.io/android/changelog.html
    implementation 'com.github.bumptech.glide:glide:4.7.1' // https://github.com/bumptech/glide
}

apply plugin: 'com.google.gms.google-services'
