apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 27
        def split = rootProject.version.toString().split('\\.')
        versionCode = (split[0].toInteger() << 16) + (split[1].toInteger() << 8) + (split[2].toInteger())
        versionName rootProject.version
    }

    signingConfigs {
        release {
            def localProperties = new Properties()
            localProperties.load(new FileInputStream(rootProject.file('local.properties')))
            storeFile file(localProperties['storeFile'])
            storePassword localProperties['storePassword']
            keyAlias localProperties['keyAlias']
            keyPassword localProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.release
            ext.enableCrashlytics = false
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    // https://developer.android.com/topic/libraries/support-library/revisions
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:preference-v14:27.1.1'
    // https://docs.fabric.io/android/changelog.html
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.2@aar') { transitive = true }
    // https://github.com/bumptech/glide
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'
    kapt 'com.github.bumptech.glide:compiler:4.7.1'
}
